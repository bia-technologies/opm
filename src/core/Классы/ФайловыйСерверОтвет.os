#Использовать semver

Перем Лог;
Перем КодСостояния Экспорт;
Перем ИмяФайла;

Процедура ПриСозданииОбъекта(Знач ПутьКФайлу)
	
	ИмяФайла = ПутьКФайлу;
	
	Файл = Новый Файл(ПутьКФайлу);
	Если Файл.Существует() Тогда
		КодСостояния = 200;
	Иначе
		
		КодСостояния = 404;
		
		Если СтрЗаканчиваетсяНа(ПутьКФайлу, ".ospx") Тогда
			ПодобратьФайлПакета(Файл.Путь);
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

Функция ПолучитьТелоКакДвоичныеДанные() Экспорт
	Возврат Новый ДвоичныеДанные(ИмяФайла);
КонецФункции

Процедура Закрыть() Экспорт
	// для совместимости контракта
КонецПроцедуры

Процедура ПодобратьФайлПакета(Знач КаталогФайловПакета)

	ИмяПакета = Новый Файл(КаталогФайловПакета).Имя;
	ИмяСРазделителем = ИмяПакета + "-";

	ФайлыПакета = НайтиФайлы(КаталогФайловПакета, "*.ospx");
	
	ВыбранныйФайл = Неопределено;
	ВыбраннаяВерсия = Неопределено;

	Для Каждого мФайлПакета Из ФайлыПакета Цикл

		ИмяСВерсией = мФайлПакета.ИмяБезРасширения;
		ТекущаяВерсия = Неопределено;

		ФайлМетаданных = Новый Файл(ОбъединитьПути(мФайлПакета.Путь, ИмяСВерсией, КонстантыOpm.ИмяФайлаМетаданныхПакета));
		Если ФайлМетаданных.Существует() Тогда
			
			// есть сохраненный файл метаданных
			МетаданныеПакета = РаботаСПакетами.ПрочитатьМетаданныеПакета(ФайлМетаданных.ПолноеИмя);
			ТекущаяВерсия = Версии.ВерсияИзСтроки(МетаданныеПакета.Свойства().Версия);

		ИначеЕсли СтрНачинаетсяС(ИмяСВерсией, ИмяСРазделителем) Тогда

			// извлекаем номер версии из имени файла
			ВерсияИзИмениФайла = Сред(ИмяСВерсией, СтрДлина(ИмяСРазделителем) + 1);
			Если Не ПустаяСтрока(ВерсияИзИмениФайла) Тогда
				ТекущаяВерсия = Версии.ВерсияИзСтроки(ВерсияИзИмениФайла);
			КонецЕсли;

		Иначе
			// Файл не по канону - пропустим
			Лог.Отладка("Файл %1 не соответствует правилам расположения файлов пакета.", мФайлПакета.ПолноеИмя);
		КонецЕсли;

		Если (ТекущаяВерсия <> Неопределено) Тогда

			Лог.Отладка("Найден файл %1 с версией %2", мФайлПакета.ПолноеИмя, ТекущаяВерсия.ВСтроку());

			Если ((ВыбраннаяВерсия = Неопределено)
				Или (ВыбраннаяВерсия.Сравнить(ТекущаяВерсия) < 0)) Тогда

				ВыбраннаяВерсия = ТекущаяВерсия;
				ВыбранныйФайл = мФайлПакета;

			КонецЕсли;

		КонецЕсли;

	КонецЦикла;

	Если ВыбранныйФайл <> Неопределено Тогда

		Лог.Отладка("Для пакета %1 подобрана версия %2", ИмяПакета, ВыбраннаяВерсия.ВСтроку());

		ИмяФайла = ВыбранныйФайл.ПолноеИмя;
		КодСостояния = 200;

	КонецЕсли;

КонецПроцедуры

Лог = Логирование.ПолучитьЛог("oscript.app.opm.fileserver");