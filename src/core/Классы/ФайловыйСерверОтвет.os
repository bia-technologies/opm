#Использовать semver

Перем КодСостояния Экспорт;
Перем ИмяФайла;

Процедура ПриСозданииОбъекта(Знач ПутьКФайлу)
	
	ИмяФайла = ПутьКФайлу;
	
	Файл = Новый Файл(ПутьКФайлу);
	Если Файл.Существует() Тогда
		КодСостояния = 200;
	Иначе
		
		КодСостояния = 404;
		
		Если СтрЗаканчиваетсяНа(ПутьКФайлу, ".ospx") Тогда
			ПодобратьФайлПакета(Файл.Путь);
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

Функция ПолучитьТелоКакДвоичныеДанные() Экспорт
	Возврат Новый ДвоичныеДанные(ИмяФайла);
КонецФункции

Процедура Закрыть() Экспорт
	// для совместимости контракта
КонецПроцедуры

Процедура ПодобратьФайлПакета(Знач КаталогФайловПакета)

	ФайлыПакета = НайтиФайлы(КаталогФайловПакета, "*.ospx");
	
	ВыбранныйФайл = Неопределено;
	ВыбраннаяВерсия = Неопределено;

	Для Каждого мФайлПакета Из ФайлыПакета Цикл

		Имя = мФайлПакета.ИмяБезРасширения;
		Части = СтрРазделить(Имя, "-");
		Если Части.Количество() > 1 Тогда
			
			ВерсияТекст = Части[Части.ВГраница()];
			ТекущаяВерсия = Версии.ВерсияИзСтроки(ВерсияТекст);

			Если (ВыбраннаяВерсия = Неопределено)
				Или (ВыбраннаяВерсия.Сравнить(ТекущаяВерсия) < 0) Тогда

				ВыбраннаяВерсия = ТекущаяВерсия;
				ВыбранныйФайл = мФайлПакета;

			КонецЕсли;

		КонецЕсли;

	КонецЦикла;

	Если ВыбранныйФайл <> Неопределено Тогда
		ИмяФайла = ВыбранныйФайл.ПолноеИмя;
		КодСостояния = 200;
	КонецЕсли;

КонецПроцедуры
