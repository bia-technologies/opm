Перем Лог;

Перем Имя Экспорт;
Перем Сервер Экспорт;
Перем КаталогПакетов Экспорт;
Перем КорневойКаталогПоиска;

Процедура ПриСозданииОбъекта(Знач ИмяСервера, Знач АдресСервера, Знач КаталогПакетовНаСервере)
	
	Имя = ИмяСервера;
	Сервер = АдресСервера;
	КаталогПакетов = КаталогПакетовНаСервере;

	Если СтрНачинаетсяС(Сервер, "file://") Тогда
		КорневойКаталогПоиска = Сред(Сервер, СтрДлина("file://") + 1);
	Иначе
		КорневойКаталогПоиска = Сервер;
	КонецЕсли;

	КорневойКаталогПоиска = ОбъединитьПути(КорневойКаталогПоиска, КаталогПакетов);

КонецПроцедуры

Функция ПолучитьИмя() Экспорт
	Возврат Имя;
КонецФункции

Функция СерверДоступен() Экспорт
	Возврат Истина;
КонецФункции

// Получает ресурс с сервера
//
// Параметры:
//	ИмяРесурса - Строка - имя файла относительно "Сервер/ПутьВХранилище"
//
// Возвращаемое значение:
// 	ФайловыйСерверОтвет - Если файл ресурса найден
//	Неопределено - Если файл ресурса не найден или возникла ошибка обращения к файлам
Функция ПолучитьРесурс(Знач ИмяРесурса) Экспорт

	ПутьКРесурсу = ОбъединитьПути(КорневойКаталогПоиска, ИмяРесурса);

	Попытка
		
		Возврат Новый ФайловыйСерверОтвет(ПутьКРесурсу);

	Исключение

		Лог.Ошибка(ОписаниеОшибки());
		Возврат Неопределено;

	КонецПопытки;

КонецФункции

Функция ПолучитьПакеты() Экспорт

	ПакетыХаба = Новый Соответствие;
	ФайлыПакетов = Новый Массив;
	Попытка
		ФайлыПакетов = НайтиФайлы(КорневойКаталогПоиска, "*.ospx", Истина);
	Исключение
		Лог.Предупреждение(
			СтрШаблон("Ошибка получения списка пакетов с хаба %1 по причине %2", 
			Имя, ОписаниеОшибки()
			)
		);
	КонецПопытки;

	Для Каждого мФайл Из ФайлыПакетов Цикл
		
		КаталогФайла = Новый Файл(мФайл.Путь);
		ИмяПакета = КаталогФайла.Имя;

		ПакетыХаба.Вставить(ИмяПакета, "");

	КонецЦикла;
	
	Возврат ПакетыХаба;

КонецФункции

Функция НастройкаДляВыгрузки() Экспорт

	Результат = Новый Структура;
	Результат.Вставить("Имя", Имя);
	Результат.Вставить("Сервер", Сервер);
	Результат.Вставить("ПутьНаСервере", КаталогПакетов);
	Результат.Вставить("РесурсПубликацииПакетов", "");
	Результат.Вставить("Порт", 0);
	Результат.Вставить("Таймаут", 0);
	Результат.Вставить("Авторизация", "");
	Результат.Вставить("Заголовки", Новый Соответствие());
	Результат.Вставить("Приоритет", 0);
	
	Возврат Результат;

КонецФункции //

Лог = Логирование.ПолучитьЛог("oscript.app.opm");
